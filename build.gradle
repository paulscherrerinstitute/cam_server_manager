plugins {
    id 'java-library'
    id "maven-publish"
    id "nebula.ospackage" version "9.1.1"
}

version = '1.7.0' 
group = 'ch.psi'
ext.timestamp = new Date().format("yyMMddHHmm")
sourceCompatibility = '1.11'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = 'ch.psi.csm.App'
}
ext.main_class = ext.mainClass

if (!hasProperty('runArgs')) {
    ext.arguments = ''
} else {
    ext.arguments = runArgs.split (" ")
} 

task(debug, dependsOn: 'classes', type: JavaExec) {      
    main = main_class
    classpath = sourceSets.main.runtimeClasspath
    args arguments
    ignoreExitValue = true
    debug true
}

task(run, dependsOn: 'classes', type: JavaExec) {      
    main = main_class
    classpath = sourceSets.main.runtimeClasspath
    args arguments 
    ignoreExitValue = true
    debug false
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task fatJar(type: Jar, dependsOn: jar) {
    classifier 'fat'
    duplicatesStrategy = 'exclude'  
    manifest {
        attributes (
            'Main-Class': mainClass,
            'Implementation-Version': version,
            'Build-Time': timestamp,
            //,'Class-Path': configurations.compile.collect { it.getName() }.join(' ')
        )
    }    
    doFirst {
        from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } } 
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'     
        
    with jar
}

repositories {
    mavenCentral()
    maven { 
            credentials {
                username = githubUser 
                password = githubPwd
            }
            url "https://maven.pkg.github.com/paulscherrerinstitute/jcae" 
        }    
        maven {
            credentials {
                username = githubUser 
                password = githubPwd
            }
            url = "https://maven.pkg.github.com/paulscherrerinstitute/bsread_java"
        }       
    maven { url "https://artifacts.psi.ch/artifactory/libs-snapshots-local/" }
}

dependencies {
    implementation 'org.epics:ca:1.2.1'
    implementation 'org.jfree:jfreechart:1.0.19'
    implementation 'com.formdev:flatlaf:3.0'
    implementation 'ch.psi:bsread:4.1.0'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:2.38'
    implementation 'org.glassfish.jersey.containers:jersey-container-grizzly2-http:2.38'
    implementation 'org.glassfish.jersey.media:jersey-media-sse:2.38'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:2.38'    
    implementation 'com.fifesoft:rsyntaxtextarea:3.3.2'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:4.6.1.201703071140-r'
    implementation 'org.slf4j:slf4j-nop:2.0.6'
    implementation 'javax.mail:javax.mail-api:1.6.2'
    implementation 'com.sun.mail:javax.mail:1.6.2'    
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.zeromq:jeromq:0.5.3' 
    testImplementation group: 'junit', name: 'junit', version: '4.10'
}


def app_name = project.name
def app_path = '/opt/' + project.name + "/" + version
def jar_file = app_path + '/lib/' + fatJar.archiveName
def sys_bin ='/usr/local/bin/'
def sys_lib ='/usr/local/lib/'

task generateRpm(type: Rpm) {
    //Commented as is being executed in clean+build
    dependsOn 'clean' 
    dependsOn 'fatJar' 

    packageName project.name
    requires 'java-11-openjdk'
    release = 1
    //version '1.0.0'    
    arch 'noarch'
    os LINUX
  
    user='root'
    permissionGroup 'root'
    addParentDirs false

    directory(app_path, 755)
    directory(app_path + '/lib', 755)
    directory(app_path + '/bin', 755)
    
    into app_path  

    from ('build/libs') {
        into 'lib'
        //include fatJar.archiveName
        fileMode 0644
    }

    from('scripts') {
        into 'bin'
        //Add version to startup script name
        rename { String fileName ->
            if (fileName == "startup"){
                app_name + "_" + version
            }
        }
        expand([
            "app_name": app_name,
            "app_version": version,
            "jar_file": jar_file
        ])
        fileMode 0755
    }

    link(sys_bin + app_name + "_" + version, app_path + '/bin/' + app_name + "_" + version)   
    link(sys_bin + app_name, sys_bin + app_name + "_" + version)   
}

publishing {
    repositories {
        maven {
            credentials {
                username artifactoryUser 
                password artifactoryPwd
            }
            url = artifactoryUrlRel
        }
    }

    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            //artifact javadocJar
            artifact fatJar
            //artifact generateRpm
            groupId group
            artifactId project.name
            version version
        }
    }
}


